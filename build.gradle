apply plugin: 'cpp'
apply plugin: 'google-test'

apply from: 'gradle/gtest_conf.gradle'

String qx(String... command){
	def stdout = new ByteArrayOutputStream()
	exec {
		commandLine = command
		standardOutput = stdout
	}
	//println "Output: $stdout"
	return stdout.toString()
}

String[] pkgConfig(String opt, List<String> pkgs){
	return qx('pkg-config', *pkgs, "--${opt}").split("\\s")
}

String[] pkgCFlags(List<String> pkgs){
	return pkgConfig('cflags', pkgs)
}

String[] pkgLibs(List<String> pkgs){
	return pkgConfig('libs', pkgs)
}

model {
	components {
		uulib(NativeLibrarySpec)
		fusepp(NativeLibrarySpec)
	}
	
	binaries {
		def requiredPackages = ['fuse']
		def cFlags = pkgCFlags(requiredPackages)
		def linkLibs = pkgLibs(requiredPackages)
		
		withType(NativeBinarySpec) {
			cppCompiler.define '_FILE_OFFSET_BITS', '64'
			cCompiler.define '_FILE_OFFSET_BITS', '64'
			if(toolChain in Gcc) {
				cppCompiler.args '-std=c++14'
				cppCompiler.args cFlags
				cCompiler.args   cFlags
				linker.args      linkLibs
			}
		}
	}
}